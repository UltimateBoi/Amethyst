buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

group = 'com.amethyst'
version = '1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    
    clientRunArgs += "--tweakClass org.spongepowered.asm.launch.MixinTweaker"
    clientJvmArgs += "-Dmixin.debug=true"
}

repositories {
    mavenCentral()
    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = 'Minecraft'
        url = 'https://libraries.minecraft.net/'
    }
}

configurations {
    implementation
    compile
    annotationProcessor
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compile 'org.spongepowered:mixin:0.8.5'
    compile 'org.ow2.asm:asm-commons:5.2'
    compile 'com.google.guava:guava:17.0'
} 

jar {
    manifest {
        attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.amethyst.json',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true'
        )
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}